plugins {
    id 'java'
    id 'eclipse'
    id 'org.springframework.boot' version '3.4.0'
    id 'io.spring.dependency-management' version '1.1.6'
}

group = 'com.app'
version = '0.0.1-SNAPSHOT'



java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

ext {
    jjwtVersion = '0.11.5'
    mapstructVersion = '1.6.2'
    lombokVersion = '1.18.32'
    awsSdkVersion = '2.25.32'
    tomcatVersion = '10.1.47'
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'src/main/generated']
        }
    }
}

dependencies {
    // --- Spring 기본 ---
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    
    // --- Tomcat 버전 명시적 지정 ---
    implementation "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}"
    implementation "org.apache.tomcat.embed:tomcat-embed-el:${tomcatVersion}"
    implementation "org.apache.tomcat.embed:tomcat-embed-websocket:${tomcatVersion}"

    // --- MyBatis & DB ---
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.3'
    runtimeOnly 'com.mysql:mysql-connector-j'
    // TimescaleDB (PostgreSQL) driver
    runtimeOnly 'org.postgresql:postgresql'
    // runtimeOnly 'com.h2database:h2' // 필요 시 주석 해제

    // --- Redis ---
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // --- JWT ---
    implementation "io.jsonwebtoken:jjwt-api:${jjwtVersion}"
    runtimeOnly  "io.jsonwebtoken:jjwt-impl:${jjwtVersion}"
    runtimeOnly  "io.jsonwebtoken:jjwt-jackson:${jjwtVersion}"

    // --- Config / dotenv ---
    implementation 'me.paulschwarz:spring-dotenv:4.0.0'

    // --- MapStruct ---
    implementation "org.mapstruct:mapstruct:${mapstructVersion}"

    // --- Lombok (컴파일 전용) ---
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"

    // --- Annotation Processors (순서 중요) ---
    // 1) Spring configuration processor
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // 2) Lombok
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    // 3) Lombok + MapStruct binding
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

    // 4) MapStruct
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    // --- Test Annotation Processors ---
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
    testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    // --- AWS SDK ---
    implementation platform("software.amazon.awssdk:bom:${awsSdkVersion}")
    implementation 'software.amazon.awssdk:s3'

    // --- DevTools ---
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0"

    // --- Kafka ---
    implementation 'org.springframework.kafka:spring-kafka'
    testImplementation 'org.springframework.kafka:spring-kafka-test'

    // (Kafka Streams까지 쓸 계획이면)
    // implementation 'org.apache.kafka:kafka-streams'
    
    // --- Test ---
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs += ['-parameters']
}
